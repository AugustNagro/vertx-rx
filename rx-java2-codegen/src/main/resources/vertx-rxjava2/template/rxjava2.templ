
@code{
  def genFutureMethodName(method) {
    return 'rx' + java.lang.Character.toUpperCase(method.name.charAt(0)) + method.name.substring(1);
  }

  def genFutureMethod(method) {
    var futMethodName = genFutureMethodName(method);
    var futParams = new java.util.ArrayList();
    var count = 0;
    var size = method.params.size() - 1;
    while (count < size) {
      var param = method.params[count];
      /* Transform ReadStream -> Flowable */
      futParams.add(param);
      count = count + 1;
    }
    var futParam = method.params[size];
    var futType = futParam.type.args[0].args[0];
    var futUnresolvedType = futParam.unresolvedType.args[0].args[0];
    var futReturnType;
    if (futUnresolvedType.kind == CLASS_VOID) {
      futReturnType = io.vertx.codegen.type.TypeReflectionFactory.create(io.reactivex.Completable);
    } else if (futUnresolvedType.nullable) {
      futReturnType = new io.vertx.codegen.type.ParameterizedTypeInfo(io.vertx.codegen.type.TypeReflectionFactory.create(io.reactivex.Maybe), false, java.util.Collections.singletonList(futType));
    } else {
      futReturnType = new io.vertx.codegen.type.ParameterizedTypeInfo(io.vertx.codegen.type.TypeReflectionFactory.create(io.reactivex.Single), false, java.util.Collections.singletonList(futType));
    }
    var futMethod = new io.vertx.codegen.MethodInfo(
      method.ownerTypes, futMethodName,
      method.kind,
      futReturnType,
      null,
      method.fluent,
      method.cacheReturn,
      futParams,
      method.comment,
      method.doc,
      method.staticMethod,
      method.defaultMethod,
      method.typeParams);
    return futMethod;
  }

  def genOverloadedMethod(method, streamType) {
    var params = null;
    var count = 0;
    for (param : method.params) {
      if (param.type.parameterized && param.type.raw.name == 'io.vertx.core.streams.ReadStream') {
        if (params == null) {
          params = new java.util.ArrayList(method.params);
        }
        var paramType = new io.vertx.codegen.type.ParameterizedTypeInfo(
          io.vertx.codegen.type.TypeReflectionFactory.create(streamType),
          false,
          java.util.Collections.singletonList(param.type.args[0])
        );
        params[count] = new io.vertx.codegen.ParamInfo(
          param.index,
          param.name,
          param.description,
          paramType
        );
      }
      count = count + 1;
    }
    if (params != null) {
      return new io.vertx.codegen.MethodInfo(
        method.ownerTypes,
        method.name,
        method.kind,
        method.returnType,
        null,
        method.fluent,
        method.cacheReturn,
        params,
        method.comment,
        method.doc,
        method.staticMethod,
        method.defaultMethod,
        method.typeParams);
    }
    return null;
  }
}

@declare{'genReadStream'}
  io.reactivex.Observable<@{typeParams[0].name}> toObservable();\n\n
  io.reactivex.Flowable<@{typeParams[0].name}> toFlowable();\n\n
@end{}

@declare{'genToXXXAble'}
  public synchronized io.reactivex.@{rxType}<@{streamType.simpleName}> to@{rxType}() {\n
    if (@{rxName} == null) {\n
	@if{streamType.kind == CLASS_API}
      java.util.function.Function<@{streamType.raw.name}, @{streamType.simpleName}> conv = @{streamType.raw.simpleName}::newInstance;\n
      @{rxName} = io.vertx.reactivex.@{rxType}Helper.to@{rxType}(delegate, conv);\n
	@else{streamType.variable}
      java.util.function.Function<Object, @{streamType.simpleName}> conv = (java.util.function.Function<Object, @{streamType.simpleName}>) __typeArg_0.wrap;\n
      @{rxName} = io.vertx.reactivex.@{rxType}Helper.to@{rxType}(delegate, conv);\n
	@else{}
      @{rxName} = io.vertx.reactivex.@{rxType}Helper.to@{rxType}(this.getDelegate());\n
	@end{}
    }\n
    return @{rxName};\n
  }\n\n
@end{}

@declare{'genToObservable'}
	@code{streamType=type.raw.readStreamArg}
  private io.reactivex.Observable<@{streamType.simpleName}> observable;\n
  private io.reactivex.Flowable<@{streamType.simpleName}> flowable;\n\n
	@includeNamed{'genToXXXAble';rxType='Observable';rxName='observable'}
	@includeNamed{'genToXXXAble';rxType='Flowable';rxName='flowable'}
@end{}

@declare{'genRxMethod'}
	@code{futMeth = genFutureMethod(method)}
	@code{adapterType='io.vertx.reactivex.impl.AsyncResult' + futMeth.returnType.raw.simpleName + '.to' + futMeth.returnType.raw.simpleName}
	@code{rxType=futMeth.returnType.raw.name}
	@includeNamed{'startMethodTemplate';meth=futMeth;deprecated=''} { \n
    return @{adapterType}(handler -> {\n
      @{method.name}(@foreach{param:futMeth.params}@{param.name}@end{', '}@if{futMeth.params.size() > 0}, @end{}handler);\n
    });\n
  }\n\n
@end{}

@declare{'genRxImports'}
import io.reactivex.Observable;\n
import io.reactivex.Flowable;\n
import io.reactivex.Single;\n
import io.reactivex.Completable;\n
import io.reactivex.Maybe;\n
@end{}

@declare{'genMethods'}
	@includeNamed{'genMethod'}
	@code{flowableOverload = genOverloadedMethod(method, io.reactivex.Flowable)}
	@code{observableOverload = genOverloadedMethod(method, io.reactivex.Observable)}
	@if{flowableOverload != null}
		@includeNamed{'genMethod';method=flowableOverload}
	@end{}
	@if{observableOverload != null}
		@includeNamed{'genMethod';method=observableOverload}
	@end{}
@end{}

@includeNamed{'class.templ';rxid='reactivex';rxpkg='rx'}
